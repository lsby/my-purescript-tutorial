# 函子

函子是一个类型类:

```Haskell
class Functor f where
  map :: forall a b. (a -> b) -> f a -> f b
```

要理解它, 必须先理解`函子想描述什么`, 即, 它的模型.

其实它有很复杂的数学模型, 但我们只说简单的.

## 盒子比喻

考虑一个形如`F a`的类型, 比如`Array Int`.

把它想象成一个盒子, 盒子里面放着一个值.

对`Array Int`来说, 就是一个叫`Array`的盒子, 里面放着一个`Int`类型的值.

那么函子能提供什么功能呢? 它允许你输入一个`a→b`的函数, 来修改盒子里的值.

对于`Array Int`而言, 就是输入一个`Int→X`类型的函数, 然后你就会得到`Array X`.

从`map`函数的签名也可以看出, 输出`a→b`的函数, 然后输入`f a`, 得到`f b`.

数组实现了函子类型类, 所以可以使用`map`处理数组:

```Haskell
f1 :: Int -> String
f1 a = show a

x5 :: Array Int
x5 = [ 1, 2, 3 ]

x6 :: Array String
x6 = map f1 x5
```

## 上下文

虽然, 实际上`x5`里面有三个`Int`值, 但这不重要, 我们就把他理解为, 一个`Int`值.

那么`map`做了什么呢? 它把这个`Int`值转成了一个字符串.

所以我们得到了`Array String`, 意为一个叫`Array`的盒子里装了一个`String`值.

但为什么要这么看, 它明明是三个值啊?

粗略的说, 对于形如`F a`类型的值, 在函子的模型中, 说是一个`a`类型的值放入了一个叫`F`的上下文中.

上下文可以给值提供更多的能力.

在这里, `Array`上下文提供的能力是`不确定性`.

日常生活中也有这样的例子, 比如说, x是`1或者2`, 那么这里就可以写成`x=[1,2]`.

那, `x+1`是多少? 答案就是`2或者3`了.

可以使用`map`表达这个过程: `map (\a → a + 1) x`.

这个的意思是, 对这个`Array`盒子里的, 那个不确定的数加一. 得到的结果就是`[2,3]`.

> 所以理解模型很重要, 如果不理解模型, 就不能解释为什么会得到这个结果.
> 虽然可以通过看代码找到他的逻辑, 可以扣逻辑得到结果, 但却不知道为什么要这样实现.
> 而如果理解了模型, 一切都是自然的, 就不需要再在意它具体是怎么写的, 就可以自然的使用了.

一个单纯的`Int`类型的值是不具有表示不确定性的能力的, 但只要套在`Array`里就可以.

但数组还可以实现其他类型类, 在其他类型类中, 它不一定会表示不确定性, 有时候我们也会把数组看作很多值组成的序列, 我们也有处理序列的函数.

## Maybe

另一个常见的模型是`Maybe`.

```Haskell
data Maybe a
  = Just a
  | Nothing

instance Functor Maybe where
  map :: forall a b. (a -> b) -> Maybe a -> Maybe b
  map _ Nothing = Nothing
  map f (Just x) = Just (f x)
```

它的上下文是`可空`.

例如, 一个`Maybe Int`值, 表示的是一个叫`Maybe`的盒子里, 可能装着一个`Int`值, 也可能是空的.

无论是不是空的, 你都可以通过`map`操作盒子内部的值(如果值存在的话).

逻辑显而易见, 如果是空盒子, 怎么操作都是空盒子, 如果是有值的盒子, 那么值就会变.

这非常适合处理一些可能出错的情况. 比如, 一个除法函数:

```Haskell
div :: Number -> Number -> Maybe Number
div a b
  | b == 0.0 = Nothing
  | otherwise = Just (a / b)
```

我们知道, 除法的除数不能为0, 这里就可以说, 除法返回的结果可能是一个空, 或者一个`Number`.

接下来就可以使用了:

```Haskell
x7 :: Maybe Number
x7 = div 2.0 1.0

x11 :: Maybe Number
x11 = div 2.0 0.0
```

对于任何一个`Maybe Number`类型的值, 我们也可以像普通的, 操作`Number`类型的值一样操作它.

比如我们有一个操作`Number`类型的函数.

```Haskell
add1 :: Number -> Number
add1 a = a + 1.0
```

现在只要在函数前面加`map`就可以用在`Maybe Number`类型的值上了:

```Haskell
x8 :: Maybe Number
x8 = map add1 x7
```

最后, 在真正要使用这个`Maybe`值的时候, 只需要模式匹配一下, 把盒子打开就可以了:

```Haskell
maybeNumberToStr :: Maybe Number -> String
maybeNumberToStr (Nothing) = "Nothing"
maybeNumberToStr (Just a) = show a

x9 :: String
x9 = maybeNumberToStr x8

x10 :: String
x10 = maybeNumberToStr $ map add1 (div 2.0 0.0)
```

### 完整代码

```haskell
module Main where

import Prelude
import Effect (Effect)
import Effect.Console (log)

data Maybe a
  = Just a
  | Nothing

instance Functor Maybe where
  map :: forall a b. (a -> b) -> Maybe a -> Maybe b
  map _ Nothing = Nothing
  map f (Just x) = Just (f x)

div :: Number -> Number -> Maybe Number
div a b
  | b == 0.0 = Nothing
  | otherwise = Just (a / b)

x7 :: Maybe Number
x7 = div 2.0 1.0

x11 :: Maybe Number
x11 = div 2.0 0.0

add1 :: Number -> Number
add1 a = a + 1.0

maybeNumberToStr :: Maybe Number -> String
maybeNumberToStr (Nothing) = "Nothing"
maybeNumberToStr (Just a) = show a

x8 :: Maybe Number
x8 = map add1 x7

x9 :: String
x9 = maybeNumberToStr x8

x10 :: String
x10 = maybeNumberToStr $ map add1 (div 2.0 0.0)

main :: Effect Unit
main = log x10
```

